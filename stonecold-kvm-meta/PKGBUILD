# Maintainer: StoneCold <forumi0721[at]gmail[dot]com>

_localpkgver=Y
pkgname=("stonecold-kvm-meta")
pkgver=20150315.1
pkgrel=1
pkgdesc="Install kvm packages"
arch=("x86_64")
url="http://localhost/"
license=("custom")
depends=(
  "qemu"
  "polkit"
  "libvirt"
  "virt-manager"
  "vte290"
)
depends+=(
  "binfmt-qemu-static"
  "qemu-static"
)
install="${pkgname}.install"
source=("50-org.libvirt.unix.manage.rules")
md5sums=("SKIP")

pkgver() {
  local date="$(date +'%Y%m%d')"
  local revision="1"
  local pkgarch="${arch}"
  if [ "${pkgarch}" != "any" ]; then
    pkgarch="${CARCH}"
  fi
  if [ "$(echo "${pkgver}" | cut -d '.' -f 1)" = "${date}" ]; then
    revision="$(echo "${pkgver}" | cut -d '.' -f 2)"
  fi
  for pkg in $(ls "${startdir}"/${pkgname}-${date}.*-${pkgrel}-*.pkg.tar.* 2> /dev/null)
  do
    local prevpkgrev="$(echo "${pkg}" | sed 's/^\(.*\)-\(.*\)\.\(.*\)-\(.*\)-\(.*\)\.pkg\.tar\..*$/\3/g')"
    if [ "${prevpkgrev}" -ge "${revision}" ]; then
      revision="${prevpkgrev}"
    fi
  done

  local prevpkg=$(ls "${startdir}"/${pkgname}-${date}.${revision}-${pkgrel}-${pkgarch}.pkg.tar.* 2> /dev/null)
  if [ ! -z "${prevpkg}" ]; then
    revision="$((${revision} + 1))"
  fi

  echo "${date}.${revision}"
}

package() {
  install -dm700 "${pkgdir}"/etc/polkit-1/rules.d
  install -Dm644 "${srcdir}"/50-org.libvirt.unix.manage.rules "${pkgdir}"/etc/polkit-1/rules.d/

  chown 102 "${pkgdir}"/etc/polkit-1/rules.d
}

# vim:set ts=2 sw=2 et:
